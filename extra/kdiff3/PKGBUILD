# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to explicitly link v5/v6 with libatomic

pkgname=kdiff3
pkgver=1.8.5
pkgrel=1
pkgdesc="A file comparator/merge tool"
arch=(x86_64)
url="https://kde.org/applications/development/kdiff3/"
license=(GPL)
depends=(kparts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        0001-link-atomic.patch)
sha256sums=('bc92f7d04eb123f9c56dbe3047af67bf715240fa99487ac1d1ccf2a45e0854c1'
            'SKIP'
            '414fd602932d4572eb564da71c8adde822dfcc96727693c266195580a04d9809')
validpgpkeys=(EE56D710885D36FB1E4D21F3F442B36D614B06BC) # Michael Reeves <reeves.87@gmail.com>

prepare() {
  if [[ $CARCH == "armv6h" || $CARCH == "arm" ]]; then
    cd $pkgname-$pkgver
    patch -p1 -i ../0001-link-atomic.patch
  fi
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
