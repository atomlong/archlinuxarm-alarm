# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Thomas Jost <schnouki@schnouki.net>
# Contributor: JaDa <jada@archlinux.us>
# Contributor: Joaquim Pedro <osmano807@gmail.com>
# Contributor: Jan Rüegg <rggjan@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - hack to skip using v7 assembly on v6
#  - depend on libunwind for AArch64

pkgname=gperftools
pkgver=2.8.1
pkgrel=1
pkgdesc="Fast, multi-threaded malloc and nifty performance analysis tools"
arch=('x86_64')
url="https://github.com/gperftools/gperftools"
license=('BSD')
depends=('perl')
depends_aarch64=('libunwind')
provides=('libtcmalloc.so'
          'libprofiler.so'
          'libtcmalloc_debug.so'
          'libtcmalloc_and_profiler.so'
          'libtcmalloc_minimal.so'
          'libtcmalloc_minimal_debug.so')
optdepends=('graphviz: pprof graph generation'
            'gv: pprof postscript generation')
source=(https://github.com/gperftools/gperftools/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz
        "armv6.patch")
sha256sums=('12f07a8ba447f12a3ae15e6e3a6ad74de35163b787c0c7b76288d7395f2f74e0'
            '5a6b7c43ef4c916df930a6a59bef3285d19faffcdb6107a29a96552e53d243ee')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../armv6.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-frame-pointers
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
