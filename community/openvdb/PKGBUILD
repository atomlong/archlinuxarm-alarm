# Maintainer : Sven-Hendrik Haase <svenstaro@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=openvdb
pkgver=7.2.0
pkgrel=1
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=('x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib' 'jemalloc' 'blosc' 'log4cplus' 'openexr')
makedepends=('doxygen' 'boost' 'cmake' 'mesa' 'cppunit' 'glfw-x11' 'glu' 'python' 'python-numpy' 'ninja')
optdepends=('glfw: for tools'
            'glu: for tools'
            'python-numpy: python module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dreamworksanimation/openvdb/archive/v${pkgver}.tar.gz"
        https://patch-diff.githubusercontent.com/raw/AcademySoftwareFoundation/openvdb/pull/905.patch)
sha512sums=('f55ce585349272ddf534d2ccdf51ebbb1b50ac0a0431056c8f4d431cc6867e31cc0abdc3ea7856918904fc114406d896e89ee1b52be0eb6626cbf7ad9ac90160'
            '034a96b1baa59b4455d46244a3dbbf85ca775e6295ed77b968de10d56816af8bc2bdf9c5f29ce949edfbaea780fbbba07be4e1b6f303946b9af9f130f5c8fed0')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # sed -i "s/MINIMUM_PYTHON_VERSION 2.7/MINIMUM_PYTHON_VERSION 3.7/g" CMakeLists.txt
  patch -Np1 -i "${srcdir}/905.patch"

  mkdir build
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"/build

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LDFLAGS="-latomic $LDFLAGS"
  cmake .. \
    -GNinja \
    -DUSE_NUMPY=ON \
    -DUSE_LOG4CPLUS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENVDB_BUILD_PYTHON_MODULE=ON \
    -DOPENVDB_BUILD_DOCS=ON \
    -DOPENVDB_BUILD_UNITTESTS=OFF

  sed -i "s/isystem/I/g" build.ninja
  ninja
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  DESTDIR="${pkgdir}" ninja install
  mkdir -p "${pkgdir}"/usr/share/doc/
  mv "${pkgdir}"/usr/doc "${pkgdir}"/usr/share/doc/$pkgname
}

# vim:set sw=2 sts=2 et:
