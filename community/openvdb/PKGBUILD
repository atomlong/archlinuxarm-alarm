# Maintainer : Sven-Hendrik Haase <svenstaro@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=openvdb
pkgver=7.2.1
pkgrel=1
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=('x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib' 'jemalloc' 'blosc' 'log4cplus' 'openexr')
makedepends=('doxygen' 'boost' 'cmake' 'mesa' 'cppunit' 'glfw-x11' 'glu' 'python' 'python-numpy' 'ninja')
optdepends=('glfw: for tools'
            'glu: for tools'
            'python-numpy: python module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dreamworksanimation/openvdb/archive/v${pkgver}.tar.gz")
sha512sums=('ae74c3e54fe5ae5866bc58cf83c0ba291a32dd9af9c87cbf201af7ab169b44af2afc134eaa0c7c37de0471ebde08e1ee325e4e111d69579ad9460892055fb137')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LDFLAGS="-latomic $LDFLAGS"
  cmake \
    -Bbuild \
    -GNinja \
    -DUSE_NUMPY=ON \
    -DUSE_LOG4CPLUS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENVDB_BUILD_PYTHON_MODULE=ON \
    -DOPENVDB_BUILD_DOCS=ON \
    -DOPENVDB_BUILD_UNITTESTS=OFF

  ninja -C build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  DESTDIR="${pkgdir}" ninja install
  mkdir -p "${pkgdir}"/usr/share/doc/
  mv "${pkgdir}"/usr/doc "${pkgdir}"/usr/share/doc/$pkgname
}

# vim:set sw=2 sts=2 et:
