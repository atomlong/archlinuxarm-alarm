# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - strip -march=native from extras Makefile

pkgname=ncmpcpp
pkgver=0.9
pkgrel=1
pkgdesc='Almost exact clone of ncmpc with some new features'
url='https://ncmpcpp.rybczak.net/'
arch=('x86_64')
license=('GPL')
depends=('curl' 'libcurl.so' 'libmpdclient' 'libmpdclient.so' 'fftw' 'libfftw3.so' 'boost-libs'
         'libboost_filesystem.so' 'libboost_locale.so' 'libboost_program_options.so'
         'libboost_regex.so' 'libboost_thread.so' 'icu' 'libicui18n.so' 'libicuuc.so'
         'glibc' 'gcc-libs' 'ncurses' 'libncursesw.so' 'readline' 'libreadline.so' 'taglib')
makedepends=('boost')
source=(https://rybczak.net/ncmpcpp/stable/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('aadf4bb510ce91b2af106d4881c90b618dbc30583c745e3ff924fc6c87553195b6cfb7d0034db7cf39d7001599a742d29d73a7de2701b70f36ad42d657df10e0')
b2sums=('bdc55f16079d54b6ffdc0c1562b5cc91e1ea6c22bb45fe29642f8cedc8064d087a91fff37e04ec0f858163115cce50aa2e15bfd73a406ae58983ef53e11e6c39')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
  sed -i 's/-march=native//' extras/Makefile
  sed -e 's/CXXFLAGS=/CXXFLAGS+=/' \
      -e 's/CPPFLAGS=/CPPFLAGS+=/' \
      -e 's/LDFLAGS=/LDFLAGS+=/' \
      -i extras/Makefile
}

build() {
  cd ${pkgname}-${pkgver}

  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  export CPPFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

  BOOST_LIB_SUFFIX='' ./configure \
    --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-visualizer \
    --with-fftw \
    --with-taglib
  make
  make -C extras
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 extras/artist_to_albumartist -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
