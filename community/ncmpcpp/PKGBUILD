# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - strip -march=native from extras Makefile
#  - configure with --without-lto

pkgname=ncmpcpp
pkgver=0.9.1
pkgrel=1
pkgdesc='Almost exact clone of ncmpc with some new features'
url='https://ncmpcpp.rybczak.net/'
arch=('x86_64')
license=('GPL')
depends=('curl' 'libcurl.so' 'libmpdclient' 'libmpdclient.so' 'fftw' 'libfftw3.so' 'boost-libs'
         'libboost_filesystem.so' 'libboost_locale.so' 'libboost_program_options.so'
         'libboost_regex.so' 'libboost_thread.so' 'icu' 'libicui18n.so' 'libicuuc.so'
         'glibc' 'gcc-libs' 'ncurses' 'libncursesw.so' 'readline' 'libreadline.so' 'taglib')
makedepends=('boost')
source=(https://rybczak.net/ncmpcpp/stable/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('8fa9f512ac09c144eab4e4721a9c54f3f411593fe13c26db12fbaa3ccf6b08d7cd54923e1e4148b2db45dc69431280611b2be2d153513c0d38eaf92af8ceb9fb')
b2sums=('89f6a6d68baf54b0a0eca6d3df96dcd5bcd1346e6f93d0a580f2fbbe4b4246218b62e5332f3db2567c9a91ee30ceb367039a328fa7f491c2a756c2c03fed76c3')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
  sed -i 's/-march=native//' extras/Makefile
  sed -e 's/CXXFLAGS=/CXXFLAGS+=/' \
      -e 's/CPPFLAGS=/CPPFLAGS+=/' \
      -e 's/LDFLAGS=/LDFLAGS+=/' \
      -i extras/Makefile
}

build() {
  cd ${pkgname}-${pkgver}

  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  export CPPFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

  BOOST_LIB_SUFFIX='' ./configure \
    --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-visualizer \
    --with-fftw \
    --with-taglib \
    --without-lto
  make
  make -C extras
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 extras/artist_to_albumartist -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
