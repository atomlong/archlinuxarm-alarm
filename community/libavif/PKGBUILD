# Maintainer: Bruno Pagani <archange@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove depends on svt-av1
#  - set -DAVIF_CODEC_SVT=OFF

pkgname=libavif
pkgver=0.8.4
pkgrel=1
pkgdesc="Library for encoding and decoding .avif files"
arch=(x86_64)
url="https://github.com/AOMediaCodec/libavif"
license=(BSD)
depends=(glibc aom dav1d rav1e zlib libpng libjpeg libyuv)
makedepends=(cmake nasm pkgconf gdk-pixbuf2)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('116b46a9a497c6ef178c5a20b912d8ac02f888dc8c537e40b5d16700823a9b16')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DAVIF_BUILD_APPS=ON \
    -DAVIF_CODEC_AOM=ON \
    -DAVIF_CODEC_DAV1D=ON \
    -DAVIF_CODEC_RAV1E=ON \
    -DAVIF_CODEC_SVT=OFF \
    -DAVIF_BUILD_GDK_PIXBUF=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
