# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix 32-bit FTBFS with new musl

pkgname=busybox
pkgver=1.32.1
pkgrel=3
pkgdesc="Utilities for rescue and embedded systems"
arch=("x86_64")
url="https://www.busybox.net"
license=('GPL')
makedepends=("ncurses" "musl" "kernel-headers-musl")
validpgpkeys=('C9E9416F76E610DBD09D040F47B70C55ACC9965B')
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"{,.sig}
        '0001-ash-fix-unset_var-pattern-repl.patch'
        "config"
        "0001-comment-time-size-check.patch")
sha256sums=('9d57c4bd33974140fd4111260468af22856f12f5b5ef7c70c8d9b75c712a0dee'
            'SKIP'
            '4e4fb268c51e378a3ad0a12f903a601a136d31cae8b684a51ebabbc9a6cf9250'
            '0b1df86c2fd0c8869c67096777960bddf7db2c54efab5ad890fc83e1b3cbf027'
            'd390c798540bf4369da42a575369e737e3e270c7d3c6c2bd141d1da939f4d219')
b2sums=('b0258345d40628d8c12b4cc5c3efdb318cfb469e029242942cdad22aeec5142963291a746fbac450b43a4a1f2f7e9204442456691fa98f18eeaa58c70d714caf'
        'SKIP'
        '6ce36c1e467c55b3405af58b19636daaf1ead2a5a630ed93c0e86912ecccbce359df3061718f99f08204e705eb1352c00801af3f4241e470f7d64ef6367f3c7e'
        '1ad361dd163e589aa85822834b7e5ec421c1fc7bfab1e481520a8d90ee920c2f1a07709871f864173c3cfb6eacf60f54a4e2adba4e061ae1a9a883e368f1893e'
        '2a227153f936850778a772794c4eaa3b72157b76d07ecd9b491f19c690dff258e2c6a755ef2372136235a5220688847e9357812e95d8bdc5550593831b2d592d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 < ../0001-ash-fix-unset_var-pattern-repl.patch
  patch -p1 -i ../0001-comment-time-size-check.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cp "$srcdir"/config .config
  # reproducible build
  export KCONFIG_NOTIMESTAMP=1
  make CC=musl-gcc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm0755 busybox "$pkgdir"/usr/bin/busybox

  # docs
  install -Dm644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc "$pkgdir"/usr/share/doc/$pkgname/$doc
  done
}
