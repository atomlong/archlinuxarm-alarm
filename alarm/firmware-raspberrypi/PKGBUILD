# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=28

pkgname=firmware-raspberrypi
_firmcommit=b66ab26cebff689d0d3257f56912b9bb03c20567
_bluecommit=da4bfb1ecf41025eafa5b11a54fd00808808d0f1
pkgver=20201224
pkgrel=1
pkgdesc="Additional firmware for Raspberry Pi"
arch=('any')
url="https://github.com/RPi-Distro/firmware-nonfree/commits/master/brcm"
license=('custom')
options=('!strip')
source=("nonfree-$pkgver-${_firmcommit:0:10}.tar.gz::https://github.com/RPi-Distro/firmware-nonfree/archive/${_firmcommit}.tar.gz"
        "bluez-$pkgver-${_bluecommit:0:10}.tar.gz::https://github.com/RPi-Distro/bluez-firmware/archive/${_bluecommit}.tar.gz"
        'RPi-WM8804.conf')
sha256sums=('033a21d19fbdc7617b8c5b58d4be5951e29be5be787a45875b615f4d4dcf3f5b'
            'ce834fcb7d984c367d3a23344c66449d0449a39680aba57fffdf331049cc741b'
            'f978fbc40db75ba3213a4472023496d0716706eb1a6f078f207ac027c5753f43')

package() {
  install -d "$pkgdir/usr/lib/firmware/updates/brcm" "$pkgdir/usr/share/alsa/cards/"
  install -m 0644 RPi-WM8804.conf "$pkgdir/usr/share/alsa/cards/"

  _bluez=(
    BCM43430A1.hcd
    BCM4345C0.hcd
    BCM4345C5.hcd
  )
  for i in "${_bluez[@]}"; do
    install -m 0644 "bluez-firmware-$_bluecommit/broadcom/$i" "$pkgdir/usr/lib/firmware/updates/brcm/$i"
  done

  # BCM43430 on Zero/3A/3B
  # BCM43455 on 3A+/3B+/4B/CM4
  # BCM43456 on 400

  _payload=(
    brcmfmac43430-sdio.bin
    brcmfmac43430-sdio.txt
    brcmfmac43455-sdio.bin
    brcmfmac43455-sdio.clm_blob
    brcmfmac43455-sdio.txt
    brcmfmac43456-sdio.bin
    brcmfmac43456-sdio.clm_blob
    brcmfmac43456-sdio.txt
  )
  for i in "${_payload[@]}"; do
    install -m 0644 "firmware-nonfree-$_firmcommit/brcm/$i" "$pkgdir/usr/lib/firmware/updates/brcm/$i"
  done
}
